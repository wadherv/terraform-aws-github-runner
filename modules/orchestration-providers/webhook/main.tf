locals {
  packaged_runners_lambda_zip         = "${path.module}/../../../lambdas/functions/control-plane/runners.zip"
  runner_control_artifact_s3_selected = var.config.lambda.artifact.s3 != null
  runner_control_artifact = {
    zip = local.runner_control_artifact_s3_selected ? null : coalesce(
      var.config.lambda.artifact.zip,
      local.packaged_runners_lambda_zip,
    )
    s3 = {
      bucket         = local.runner_control_artifact_s3_selected ? var.lambda.artifact.s3.bucket : null
      key            = try(var.config.lambda.artifact.s3.key, null)
      object_version = try(var.config.lambda.artifact.s3.object_version, null)
    }
  }

  resolved_config = {
    prefix = var.prefix
    tags   = var.tags
    runner = merge(var.runner, var.config.runner, {
      jit_config_enabled = (
        var.config.runner.jit_config_enabled == null
        ? var.config.runner.ephemeral
        : var.config.runner.jit_config_enabled
      )
    })
    github = merge(var.github, var.config.github)
    lambda = merge(var.lambda, {
      artifact = local.runner_control_artifact
    })
    queue = merge(var.config.queue, {
      event_source_mapping = var.config.lambda.scale.up.event_source_mapping
    })
    scale_up         = var.config.lambda.scale.up
    scale_down       = var.config.lambda.scale.down
    pool             = var.config.lambda.pool
    job_retry        = var.config.job_retry
    storage_provider = var.storage_provider
    observability    = var.observability
  }

  common_tags            = local.resolved_config.tags
  lambda_tags            = merge(local.common_tags, local.resolved_config.lambda.tags)
  queue_tags             = merge(local.common_tags, local.resolved_config.queue.tags)
  observability_log_tags = merge(local.common_tags, local.resolved_config.observability.logs.tags)

  scale_up_tags        = merge(local.common_tags, local.resolved_config.scale_up.tags)
  scale_up_lambda_tags = merge(local.lambda_tags, local.resolved_config.scale_up.tags)
  scale_up_log_tags    = merge(local.observability_log_tags, local.resolved_config.scale_up.tags)
  scale_up_queue_tags  = merge(local.queue_tags, local.resolved_config.scale_up.tags)

  scale_down_tags        = merge(local.common_tags, local.resolved_config.scale_down.tags)
  scale_down_lambda_tags = merge(local.lambda_tags, local.resolved_config.scale_down.tags)
  scale_down_log_tags    = merge(local.observability_log_tags, local.resolved_config.scale_down.tags)

  pool_tags        = merge(local.common_tags, local.resolved_config.pool.tags)
  pool_lambda_tags = merge(local.lambda_tags, local.resolved_config.pool.tags)
  pool_log_tags    = merge(local.observability_log_tags, local.resolved_config.pool.tags)

  job_retry_enabled     = local.resolved_config.job_retry.enabled
  job_retry_tags        = merge(local.common_tags, local.resolved_config.job_retry.tags)
  job_retry_lambda_tags = merge(local.lambda_tags, local.resolved_config.job_retry.tags)
  job_retry_log_tags    = merge(local.observability_log_tags, local.resolved_config.job_retry.tags)
  job_retry_queue_tags  = merge(local.queue_tags, local.resolved_config.job_retry.tags)

  enable_job_queued_check = local.resolved_config.scale_up.job_queued_check_enabled == null ? !local.resolved_config.runner.ephemeral : local.resolved_config.scale_up.job_queued_check_enabled
}
