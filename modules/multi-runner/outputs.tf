
output "runners_map" {
  value = { for runner_key, runner in module.runners : runner_key => {
    launch_template_name    = runner.launch_template.name
    launch_template_id      = runner.launch_template.id
    launch_template_version = runner.launch_template.latest_version
    launch_template_ami_id  = runner.launch_template.image_id
    lambda_up               = runner.lambda_scale_up
    lambda_up_log_group     = runner.lambda_scale_up_log_group
    lambda_down             = runner.lambda_scale_down
    lambda_down_log_group   = runner.lambda_scale_down_log_group
    lambda_pool             = runner.lambda_pool
    lambda_pool_log_group   = runner.lambda_pool_log_group
    role_runner             = runner.role_runner
    role_scale_up           = runner.role_scale_up
    role_scale_down         = runner.role_scale_down
    role_pool               = runner.role_pool
    runners_log_groups      = runner.runners_log_groups
    logfiles                = runner.logfiles
    }
  }
}
output "runners_map_v2" {
  value = { for runner_key, runner in module.runner_configs : runner_key => {
    runner                 = runner.runner
    orchestration_provider = runner.orchestration_provider
    scale_up               = runner.scale_up
    scale_down             = runner.scale_down
    pool                   = runner.pool
    provider               = runner.provider
    }
  }
}

output "scale_set" {
  description = "Shared scale-set orchestration resources, or null when no runner configuration selects scale_set."
  value = length(module.orchestration_scale_set) == 0 ? null : {
    cluster                      = module.orchestration_scale_set[0].cluster
    controller_groups            = module.orchestration_scale_set[0].controller_groups
    reconciler_config_parameters = module.orchestration_scale_set[0].reconciler_config_parameters
    resolved_container_image     = module.orchestration_scale_set[0].resolved_container_image
  }
}

output "binaries_syncer_map" {
  value = { for runner_binary_key, runner_binary in module.runner_binaries : runner_binary_key => {
    lambda           = runner_binary.lambda
    lambda_log_group = runner_binary.lambda_log_group
    lambda_role      = runner_binary.lambda_role
    location         = "s3://runner_binary.bucket.id}/runner_binary.bucket.key"
    bucket           = runner_binary.bucket
  } }
}

output "webhook" {
  value = {
    gateway          = module.webhook.gateway
    lambda           = module.webhook.lambda
    lambda_log_group = module.webhook.lambda_log_group
    lambda_role      = module.webhook.role
    endpoint         = "${module.webhook.gateway.api_endpoint}/${module.webhook.endpoint_relative_path}"
    webhook          = module.webhook.webhook
    dispatcher       = local.effective_config.orchestration_provider.webhook.eventbridge.enabled ? module.webhook.dispatcher : null
    eventbridge      = local.effective_config.orchestration_provider.webhook.eventbridge.enabled ? module.webhook.eventbridge : null
  }
}

output "ssm_parameters" {
  value = {
    id             = { name = local.github_app_parameters.id.name, arn = local.github_app_parameters.id.arn }
    key_base64     = { name = local.github_app_parameters.key_base64.name, arn = local.github_app_parameters.key_base64.arn }
    webhook_secret = { name = local.github_app_parameters.webhook_secret.name, arn = local.github_app_parameters.webhook_secret.arn }
    additional_apps_manifest = local.github_app_parameters.additional_apps_manifest != null ? {
      name = local.github_app_parameters.additional_apps_manifest.name
      arn  = local.github_app_parameters.additional_apps_manifest.arn
    } : null
  }
}

output "instance_termination_watcher" {
  value = try(local.effective_config.compute_provider.aws.ec2.instance_termination_watcher.enabled, false) && local.effective_config.compute_provider.aws.ec2.instance_termination_watcher.features.spot_termination_notification_watcher.enabled ? {
    lambda           = module.instance_termination_watcher[0].spot_termination_notification.lambda
    lambda_log_group = module.instance_termination_watcher[0].spot_termination_notification.lambda_log_group
    lambda_role      = module.instance_termination_watcher[0].spot_termination_notification.lambda_role
  } : null
}

output "instance_termination_handler" {
  value = try(local.effective_config.compute_provider.aws.ec2.instance_termination_watcher.enabled, false) && local.effective_config.compute_provider.aws.ec2.instance_termination_watcher.features.spot_termination_handler.enabled ? {
    lambda           = module.instance_termination_watcher[0].spot_termination_handler.lambda
    lambda_log_group = module.instance_termination_watcher[0].spot_termination_handler.lambda_log_group
    lambda_role      = module.instance_termination_watcher[0].spot_termination_handler.lambda_role
  } : null
}
